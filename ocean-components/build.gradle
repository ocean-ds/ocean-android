apply plugin: 'maven-publish'
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.kezong.fat-aar'

def versionNameProp = ""

task loadVersionName () {
    def VERSION_NAME = 'VERSION_NAME'

    Properties props = new Properties()
    def propFile = file('version.properties')

    if (propFile.canRead()){
        props.load(new FileInputStream(propFile))

        if (props != null && props.containsKey(VERSION_NAME)) {
            versionNameProp = props[VERSION_NAME].toString()
        }
    }
}

android {
    compileSdkVersion rootProject.ext.compileSDK
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSDK
        targetSdkVersion rootProject.ext.targetSDK
        versionCode 1
        versionName versionNameProp.toString()

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

}

afterEvaluate {
    publishing {
        def gprBaseUrl = "https://maven.pkg.github.com"
        def gprRepoOwner = "ocean-ds"
        def gprRepoId = "ocean-android"

        def gprUser = System.getenv("GPR_USER")
        def gprApiKey = System.getenv("GPR_API_KEY")

        repositories {
            maven {
                name = "ocean-android"
                url = uri("$gprBaseUrl/$gprRepoOwner/$gprRepoId")
                credentials {
                    username = gprUser
                    password = gprApiKey
                }
            }
        }

        def mavenGroupId = "br.com.useblu"
        def mavenArtifactId = project.name

        publications {
            release(MavenPublication) {
                from components.release
                groupId = mavenGroupId
                artifactId = mavenArtifactId
                version = versionNameProp
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    api 'com.github.skydoves:balloon:1.4.3'
    api 'com.google.android.material:material:1.5.0-alpha01'
    api 'br.com.concrete:canarinho:2.0.1'
    api 'com.github.bumptech.glide:glide:4.12.0'
    api "androidx.appcompat:appcompat:1.3.1"
    api 'com.google.android.material:material:1.4.0'
    api 'me.relex:circleindicator:2.1.6'
    api 'org.imaginativeworld.whynotimagecarousel:whynotimagecarousel:2.0.4'
    api 'com.github.florent37:expansionpanel:1.2.4'

    embed project(path: ':ocean-tokens')
}